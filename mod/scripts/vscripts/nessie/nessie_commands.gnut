global function Nessie_Useful_Commands_Init

global function NessieCommands_EnableSuicide

struct
{
	bool killCommandsEnabled
	table<entity, float> playerNextKillCommandAllowedTime
	table<entity, bool> playerGainSpecialLoadout
} file

// BATTERY_SPAWNERS array in nessy.gnut controls availability
void function Nessie_Useful_Commands_Init()
{
	// overrides northstar default killcommand
	AddClientCommandCallback( "kill", SelfKillCommand )
	AddClientCommandCallback( "explode", SelfExplodeCommand )
	//
	AddClientCommandCallback( "dissolve", SelfDissolveCommand )
	AddClientCommandCallback( "pinkmist", SelfPinkMistCommand )
	AddClientCommandCallback( "ragdoll", SelfRagdollDeathCommand )

	AddClientCommandCallback( "kill_admin", AdminSelfKill )
	AddClientCommandCallback( "explode_admin", AdminSelfExplode )

    //AddClientCommandCallback( "suicide", OPSuicide )
	//AddClientCommandCallback( "dropweapon", PlayerDropActiveWeapon )
	AddClientCommandCallback( "selfdamage", OPSelfDamage )
	//AddClientCommandCallback( "selfgib", OPSelfGib )
	//AddClientCommandCallback( "doomedtitan", CC_SpawnDoomedTitan )
	//AddClientCommandCallback( "doomedpilottitan", CC_SpawnDoomedPilotTitan )
	AddClientCommandCallback( "special", CC_GiveSpecialLoadout )

	// callbacks
	AddCallback_OnClientConnected( OnClientConnected )
	AddCallback_OnPlayerRespawned( OnPlayerRespawned )
	AddCallback_OnPlayerGetsNewPilotLoadout( OnPlayerChangeLoadout )
}

void function NessieCommands_EnableSuicide( bool enable )
{
	file.killCommandsEnabled = enable
}

void function OnClientConnected( entity player )
{
	// kill command
	file.playerNextKillCommandAllowedTime[ player ] <- 0.0
	// special loadout
	file.playerGainSpecialLoadout[ player ] <- false
}

void function OnPlayerRespawned( entity player )
{
	if ( file.playerGainSpecialLoadout[ player ] )
		DisableBoostBar( player )
}

void function OnPlayerChangeLoadout( entity player, PilotLoadoutDef p )
{
	if ( file.playerGainSpecialLoadout[ player ] )
		GivePlayerSpecialLoadout( player )
}

bool function SelfKillCommand( entity player, array<string> args )
{
	if ( !PlayerCanUseKillCommand( player ) )
		return true

	player.Die( player, player, { damageSourceId = damagedef_suicide } )
	print( "[NESSIE] " + player.GetPlayerName() + " suicided!!" )

	KillCommandPlayerScorePunish( player )

	return true
}

bool function SelfExplodeCommand( entity player, array<string> args )
{
	if ( !PlayerCanUseKillCommand( player ) )
		return true

	player.Die( player, player, { scriptType = DF_GIB, damageSourceId = damagedef_suicide } )
	print( "[NESSIE] " + player.GetPlayerName() + " self exploded!!" )

	KillCommandPlayerScorePunish( player )

	return true
}

bool function SelfDissolveCommand( entity player, array<string> args )
{
	if ( !PlayerCanUseKillCommand( player ) )
		return true

	player.Die( player, player, { scriptType = DF_DISSOLVE, damageSourceId = damagedef_suicide } )
	print( "[NESSIE] " + player.GetPlayerName() + " self dissolved!!" )

	KillCommandPlayerScorePunish( player )

	return true
}

bool function SelfPinkMistCommand( entity player, array<string> args )
{
	if ( !PlayerCanUseKillCommand( player ) )
		return true

	player.Die( player, player, { scriptType = DF_DISSOLVE | DF_GIB, damageSourceId = damagedef_suicide } )
	print( "[NESSIE] " + player.GetPlayerName() + " self pinkmisted!!" )

	KillCommandPlayerScorePunish( player )

	return true
}

bool function SelfRagdollDeathCommand( entity player, array<string> args )
{
	if ( !PlayerCanUseKillCommand( player ) )
		return true

	player.Die( player, player, { scriptType = DF_RAGDOLL | DF_KNOCK_BACK, damageSourceId = damagedef_suicide } )
	//player.BecomeRagdoll( player.GetVelocity(), true )
	print( "[NESSIE] " + player.GetPlayerName() + " self ragdolled!!" )

	KillCommandPlayerScorePunish( player )

	return true
}

bool function PlayerCanUseKillCommand( entity player )
{
	if ( !IsAlive( player ) )
		return false

	if ( GetConVarBool( "sv_cheats" ) || GetConVarBool( "ns_allow_kill_commands" ) )
		return true // convar killcommand toggle ignore modified checks

	if ( !file.killCommandsEnabled ) // using script killcommand toggle!
		return false

	if ( Time() < file.playerNextKillCommandAllowedTime[ player ] ) // in cooldown!
		return false
	if ( GetGameState() < eGameState.Playing ) // suicide only allowed after game start!
		return false
	if ( player.IsTitan() ) // no suicide allowed for titans
		return false

	// set killcommand cooldown
	const float KILL_COMMAND_COOLDOWN = 5.0
	file.playerNextKillCommandAllowedTime[ player ] = Time() + KILL_COMMAND_COOLDOWN

	// all checks passed
	return true
}

void function KillCommandPlayerScorePunish( entity player )
{
	if ( GetConVarBool( "sv_cheats" ) || GetConVarBool( "ns_allow_kill_commands" ) )
		return // convar killcommand toggle ignore punishment

	int kills = player.GetPlayerGameStat( PGS_KILLS )
	if ( kills > 0 )
	{
		//player.SetPlayerGameStat( PGS_KILLS, kills - 1 )
		//player.SetPlayerGameStat( PGS_PILOT_KILLS, player.GetPlayerGameStat( PGS_ASSAULT_SCORE ) - 1 )
		if ( IsFFAGame() ) // for ffa
		{
			player.SetPlayerGameStat( PGS_ASSAULT_SCORE, player.GetPlayerGameStat( PGS_ASSAULT_SCORE ) - 1 )
			AddTeamScore( player.GetTeam(), -1 )
		}
	}
}

bool function AdminSelfKill( entity player, array<string> args )
{
	if( !BATTERY_SPAWNERS.contains( player.GetUID() ) )
		return false

	if ( !IsAlive( player ) )
		return true

	thread AdminHiddenDeathThink( player )
	player.Die( player, player )
	return true
}

bool function AdminSelfExplode( entity player, array<string> args )
{
	if( !BATTERY_SPAWNERS.contains( player.GetUID() ) )
		return false

	if ( !IsAlive( player ) )
		return true

	thread AdminHiddenDeathThink( player )
	player.Die( player, player, { scriptType = DF_GIB } )
	return true
}

void function AdminHiddenDeathThink( entity player )
{
	player.EndSignal( "OnDestroy" )

	// hide any death message that count happen on this player
	ForcedRespawn_SetDisabledForPlayer( player, true )
	EntityKilledEvent_SetDisabledForEntity( player, true )

	player.WaitSignal( "OnRespawned" ) // wait for respawn
	// reset to normal
	ForcedRespawn_SetDisabledForPlayer( player, false )
	EntityKilledEvent_SetDisabledForEntity( player, false )
}

bool function OPSuicide( entity player, array<string> args )
{
	if( !BATTERY_SPAWNERS.contains( player.GetUID() ) )
		return false
	if ( GetGameState() < eGameState.Playing ) // suicide only allowed after game start!
		return true

	if( IsAlive( player ) )
	{
		//player.Die( svGlobal.worldspawn, svGlobal.worldspawn, { damageSourceId = damagedef_suicide } )
		player.Die( player, player, { damageSourceId = eDamageSourceId.round_end } )
	}

	print( "[NESSIE] Admin " + player.GetPlayerName() + " killed themselves!" )
	
	return true
}

bool function PlayerDropActiveWeapon( entity player, array<string> args )
{
	if( !IsValid( player.GetActiveWeapon() ) )
		return false

	player.DropWeapon( player.GetActiveWeapon() )
	print( "[NESSIE] " + player.GetPlayerName() + " dropped weapon!" )
	return true
}

bool function OPSelfDamage( entity player, array<string> args )
{
	if( !BATTERY_SPAWNERS.contains( player.GetUID() ) )
		return false
    if( IsAlive( player ) )
	{
		// don't add "damageSourceId = damagedef_suicide", it's a kind of force kill!
        player.TakeDamage( 10000, player, player, {} ) 
		print( "[NESSIE] Admin " + player.GetPlayerName() + " self damaged!" )
	}
    return true
}

bool function OPSelfGib( entity player, array<string> args )
{
	if( !BATTERY_SPAWNERS.contains( player.GetUID() ) )
		return false
	if( IsAlive( player ) )
	{
		// don't add "damageSourceId = damagedef_suicide", it's a kind of force kill! 
		// damageType = DF_GIB will cause player's death blur become red?
        player.TakeDamage( 10000, player, player, { scriptType = DF_GIB } ) 
		print( "[NESSIE] Admin " + player.GetPlayerName() + " self gibbed!" )
	}
	return true
}

bool function CC_SpawnDoomedTitan( entity player, array<string> args )
{
	if( !BATTERY_SPAWNERS.contains( player.GetUID() ) )
		return false
	vector pos = GetPlayerCrosshairOrigin( player )
	vector angs = player.EyeAngles()
	entity titan = CreateOgre( TEAM_BOTH, pos, angs )
	DispatchSpawn( titan )

	titan.TakeDamage( titan.GetMaxHealth() + 1, null, null, 0 ) 
	//DoomTitan( titan )
	print( "[NESSIE] " + player.GetPlayerName() + " spawned a doomed titan!" )
	return true
}

bool function CC_SpawnDoomedPilotTitan( entity player, array<string> args )
{
	if( !BATTERY_SPAWNERS.contains( player.GetUID() ) )
		return false

	vector pos = GetPlayerCrosshairOrigin( player )
	vector angs = player.EyeAngles()
	entity titan = CreateOgre( TEAM_BOTH, pos, angs )
	SetSpawnOption_AISettings( titan, "npc_titan_ogre_minigun" )
	DispatchSpawn( titan )

	titan.TakeDamage( titan.GetMaxHealth() + 1, null, null, 0 ) 
	//DoomTitan( titan )
	print( "[NESSIE] " + player.GetPlayerName() + " spawned a doomed npc pilot titan!" )

	entity titanSoul = titan.GetTitanSoul()
	if ( !IsValid( titanSoul ) )
		return true

	// mark as having a pilot valid
	titanSoul.soul.seatedNpcPilot.isValid				= true
	titanSoul.soul.seatedNpcPilot.team 					= TEAM_BOTH

	titanSoul.soul.seatedNpcPilot.modelAsset 			= $"models/humans/pilots/pilot_medium_geist_m.mdl"
	titanSoul.soul.seatedNpcPilot.title 				= "PILOT"

	titanSoul.soul.seatedNpcPilot.isInvulnerable		= false

	titanSoul.soul.seatedNpcPilot.health				= 250

	return true
}

// special loadout
bool function CC_GiveSpecialLoadout( entity player, array<string> args )
{
	if ( !BATTERY_SPAWNERS.contains( player.GetUID() ) )
		return false

	array<entity> targets
	if ( args.len() > 0 )
		targets = GetSpecialLoadoutPlayers( args[0] )
	if ( targets.len() < 0 )
		return true

	foreach ( entity player in targets )
		file.playerGainSpecialLoadout[ player ] = !file.playerGainSpecialLoadout[ player ]

	return true
}

array<entity> function GetSpecialLoadoutPlayers( string targetType )
{
	array<entity> players
	switch ( targetType )
	{
		case "all":
			players = GetPlayerArray()
			break
		default:
			players = GetPlayersFromNamePiece( targetType )
			break
	}

	return players
}

array<entity> function GetPlayersFromNamePiece( string namePiece )
{
    array<entity> foundPlayers
    foreach ( entity player in GetPlayerArray() )
    {
        string name = player.GetPlayerName()
        if ( name.tolower().find( namePiece.tolower() ) != null )
            foundPlayers.append( player )
    }

    return foundPlayers
}

void function DisableBoostBar( entity player )
{
    thread DisableBoostBar_Threaded( player )
}

void function DisableBoostBar_Threaded( entity player )
{
    WaitFrame() // wait for player earn meter being set up
    if( IsValid( player ) )
        PlayerEarnMeter_SetMode( player, eEarnMeterMode.DISABLED )
}

void function GivePlayerSpecialLoadout( entity player )
{
	thread GivePlayerSpecialLoadout_Threaded( player )
}

void function GivePlayerSpecialLoadout_Threaded( entity player )
{
	WaitFrame() // wait for anyother things set up
	if ( !IsAlive( player ) )
		return
	if ( !file.playerGainSpecialLoadout[ player ] )
		return

	foreach ( entity weapon in player.GetOffhandWeapons() )
		player.TakeWeaponNow( weapon.GetWeaponClassName() )
	player.GiveOffhandWeapon( "mp_ability_grapple", OFFHAND_SPECIAL, ["zipline_gun"] )
	player.GiveOffhandWeapon( "mp_ability_heal", OFFHAND_INVENTORY, ["jump_pad", "infinite_jump_pad"] )
	player.GiveOffhandWeapon( "mp_ability_shifter", OFFHAND_ORDNANCE, ["wraith_portal", "amped_tacticals"] )
	player.GiveOffhandWeapon( "mp_weapon_grenade_gravity", OFFHAND_ANTIRODEO, ["gravity_lift"] )
}
//